@rem ***********************************************************************************************************************
@rem This script is used to Start LASR Server first and then 
@rem Auto Load the already registered Tables to LASR Server 
@rem 
@rem Author: Shatrughan Saxena
@rem Email:  Shatrughan.Saxena@sas.com 
@rem 
@rem Environment: SAS 9.4 VA 6.2 SMP on Windows
@rem
@rem History: Date Created - 3rd October 2013 - First Development
@rem 
@rem SCRIPT_HOME	- Location fo unzipped AutoLoad_LASR folder. You can set the folder according to your setup.
@rem SAS_HOME		- Set your SAS.exe path
@rem DATASET_HOME	- Folder where your datasets resides             
@rem AUTOLOAD_SCRIPT	- Location of AutoLoad Script              
@rem START_LASR_SCRIPT	- Location of script to start LASR server 
@rem AUTOLOAD_LOGFILE	- Location of log file generated by AutoLoad script               
@rem START_LASR_LOGFILE	- Location of log file generated by Star_LASR script  
@rem
@rem *************************************************************************************************************************

echo off

setlocal EnableDelayedExpansion

@rem ********************* Set Environment Variables as per your VA Setup ****************************************************

SET SCRIPT_HOME=D:\opt\sasinside\DemoData\AutoLoad_LASR
SET SAS_HOME=D:\opt\sasinside\SASHome\SASFoundation\9.4
SET DATASET_HOME=D:\opt\sasinside\DemoData\TinyData

SET AUTOLOAD_SCRIPT=%SCRIPT_HOME%\AutoLoad_LASR.sas
SET AUTOLOAD_LOGFILE=%SCRIPT_HOME%\Logs\AutoLoad_LASR_#Y.#m.#d_#H.#M.#s.log
SET START_LASR_SCRIPT=%SCRIPT_HOME%\Start_LASR.sas
SET START_LASR_LOGFILE=%SCRIPT_HOME%\Logs\Start_LASR_#Y.#m.#d_#H.#M.#s.log

echo Checking LASR Server Status..........

@rem ********************* Checking LASR Server Status ***********************************************************************

netstat -o -n -a | findstr 0.0.0.0:10010 
if %ERRORLEVEL%==0 (goto AUTOLOAD_SECTION) else (goto START_LASR)

@rem ********************* Starting LASR Server *******************************************************************************

:START_LASR

echo Starting LASR

START "cmd" %SAS_HOME%\sas.exe -sysin "%START_LASR_SCRIPT%" -log "%START_LASR_LOGFILE%" -batch -noterminal -logparm "rollover=session" -memsize MAX

powershell -command "Start-Sleep -s 5"

if %ERRORLEVEL%==0 (goto AUTOLOAD_SECTION) else (goto LASR_START_FAIL)

echo LASR Started

:AUTOLOAD_SECTION

@rem ********************* Starting AutoLoad_LASR.sas script *********************************************************************

echo Code reached loading datasets

START /wait "cmd" %SAS_HOME%\sas.exe -sysin "%AUTOLOAD_SCRIPT%" -log "%AUTOLOAD_LOGFILE%" -batch -noterminal -logparm "rollover=session"

echo Code finished loading datasets. checking for errors

if %ERRORLEVEL%==0 (goto EndOfScript) else (goto AUTOLOAD_DATASETS_FAILED)

goto EndOfScript

:NoDataSets
echo No Datasets Exists in this folder %DATASET_HOME% . Please load datasets into the folders and run this script again.
echo Exiting.......
pause
exit

:LASR_START_FAIL
echo LASR Server failed to start. Please check the logs, resolve the issues and run the script again.
pause
exit

:AUTOLOAD_DATASETS_FAILED
echo Autoloading Datasets failed. Please check the logs, resolve the issues and run the script again.
pause
exit

:EndOfScript
echo Script Ended well....exiting......
pause
exit